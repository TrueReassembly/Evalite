plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id "io.github.goooler.shadow" version "8.1.7"
}

group = 'dev.reassembly'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }

}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "gg.ingot:iron:1.2.1"
    implementation "com.github.shynixn.mccoroutine:mccoroutine-bukkit-api:2.17.0"
    implementation "com.github.shynixn.mccoroutine:mccoroutine-bukkit-core:2.17.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0-RC"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.3'
}

def targetJavaVersion = 21
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
kotlin {
    jvmToolchain(21)
}

compileKotlin {
    kotlinOptions.javaParameters = true
}

shadowJar {
    relocate 'co.aikar.commands', 'dev.reassembly.evalite.acf'
    relocate 'co.aikar.locales', 'dev.reassembly.evalite.locales'
}